plugins {
    id 'java'
    id 'org.openapi.generator' version '6.5.0'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.openapi.generator'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    openApiGenerate {
        // see https://github.com/OpenAPITools/openapi-generator/blob/master/modules/openapi-generator-gradle-plugin/README.adoc
        generatorName = "spring"
        inputSpec = "$projectDir/api.yaml"
        validateSpec = true
        outputDir = "$rootDir"
        configFile = "$rootDir/openapi-code-generator/config-spring.json"
    }

//    At the moment there doesn't seem to be a way to get open api generator to create the openApi config object but not generate main and all the controllers.
//    So we create main and all the controllers but then delete them immediately after.
    task cleanUpUnwantedGeneratedFiles(type: Delete) {
        print(rootDir)
        delete files("$rootDir/src/test", "$rootDir/src/main/java/org/openapitools/OpenApiGeneratorApplication.java")
        delete fileTree("$rootDir/src/main/java/com/vmware/taurus/controlplane/model/api") {
            include '**/*Controller.java'
        }
        doLast {

            fileTree("$rootDir/src/main/java/com/vmware/taurus/controlplane/model/api") {
                    }.each { File propFile ->
                String content = propFile.getText()
                content = content
                        .replace('"DataJobsDeployment"','"Data Jobs Deployment"')
                        .replace('"DataJobs"','"Data Jobs"')
                        .replace('"DataJobsExecution"','"Data Jobs Execution"')
                        .replace('"DataJobsProperties"','"Data Jobs Properties"')
                        .replace('"DataJobsService"','"Data Jobs Service"')
                        .replace('"DataJobsSources"','"Data Jobs Sources"')
                propFile.setText(content)
            }
        }
    }

    task buildPython(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
        generatorName = "python-nextgen"
        inputSpec = "$projectDir/api.yaml"
        outputDir = "$projectDir/build/python"
        configFile = "$projectDir/config-python.json"
        doLast {

            fileTree("$projectDir/build/python/taurus_datajob_api/api") {
            }.each { File propFile ->
                if(propFile.getName().endsWith("data_jobs_sources_api.py")) {
                    // Everything in this section can be removed when the ticket(https://github.com/OpenAPITools/openapi-generator/issues/15327) is closed
                    String content = propFile.getText()
                    content = content
                            .replace(
                                    'def sources_upload(self, team_name : Annotated[StrictStr, Field(..., description="Team Name")], job_name : Annotated[StrictStr, Field(..., description="Data Job Name.")], body : StrictStr, reason : Annotated[Optional[StrictStr], Field(description="The reason for executing the request")] = None, **kwargs) -> DataJobVersion:  # noqa: E501',
                                    'def sources_upload(self, team_name : Annotated[StrictStr, Field(..., description="Team Name")], job_name : Annotated[StrictStr, Field(..., description="Data Job Name.")], body : bytes, reason : Annotated[Optional[StrictStr], Field(description="The reason for executing the request")] = None, **kwargs) -> DataJobVersion:  # noqa: E501').
                            replace('def sources_upload_with_http_info(self, team_name : Annotated[StrictStr, Field(..., description="Team Name")], job_name : Annotated[StrictStr, Field(..., description="Data Job Name.")], body : StrictStr, reason : Annotated[Optional[StrictStr], Field(description="The reason for executing the request")] = None, **kwargs):  # noqa: E501',
                                    'def sources_upload_with_http_info(self, team_name : Annotated[StrictStr, Field(..., description="Team Name")], job_name : Annotated[StrictStr, Field(..., description="Data Job Name.")], body : bytes, reason : Annotated[Optional[StrictStr], Field(description="The reason for executing the request")] = None, **kwargs):  # noqa: E501')
                    propFile.setText(content)
                }else if(propFile.getName().endsWith("data_jobs_properties_api.py")) {
                    // Everything in this section can be removed when the ticket(https://github.com/OpenAPITools/openapi-generator/issues/15328) is closed
                    String content = propFile.getText()
                    content = content.
                            replace("if _params['request_body']:", "if _params['request_body'] is not None:")
                    propFile.setText(content)
                }
            }
        }
    }
    cleanUpUnwantedGeneratedFiles.dependsOn 'openApiGenerate'
    compileJava.dependsOn 'openApiGenerate' //generate Java from openapi specs
}

compileJava.dependsOn 'datajob-api:cleanUpUnwantedGeneratedFiles'
