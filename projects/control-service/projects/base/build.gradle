// Based on https://start.spring.io/ configuration

plugins {
   id 'java-library'
   id 'io.spring.dependency-management'
   id 'org.springframework.boot'
   // https://projectlombok.org/setup/gradle
   id 'io.freefair.lombok'
   id 'jacoco'
   id 'org.barfuin.gradle.jacocolog'
   id 'com.github.jk1.dependency-license-report'
}

apply from: '../java-common.gradle'
apply from: '../application.gradle'
apply from: '../maven.gradle'
apply from: '../versions-of-external-dependencies.gradle'

bootJar {
   enabled = false
}

jar {
   enabled = true
}

version = file('version.txt').text.trim()

test {
   useJUnitPlatform()
}

configurations {
   testImplementation.exclude group: 'com.vaadin.external.google', module: 'android-json'
}

// completely remove log4j as per CVE-2021-44228, also it is not needed as we are using logback
configurations.all {
   exclude group: 'org.apache.logging.log4j'
}

dependencies {
   api 'org.springframework.boot:spring-boot-starter-actuator'

   //Disable security export until stable
   implementation 'org.springframework.boot:spring-boot-starter-security'
   implementation 'org.springframework.security:spring-security-oauth2-resource-server'
   implementation 'org.springframework.security:spring-security-oauth2-jose'
   api 'org.springframework.boot:spring-boot-starter-web'
   implementation 'org.springframework.security.kerberos:spring-security-kerberos-web:1.0.1.RELEASE'

   implementation "org.springframework:spring-aspects"

   implementation versions.'com.google.guava:guava'
   implementation versions.'io.micrometer:micrometer-registry-prometheus'
   implementation versions.'org.apache.commons:commons-lang3'

   implementation versions.'com.nimbusds:nimbus-jose-jwt' // necessary for authZ ; remove after
   implementation 'javax.annotation:javax.annotation-api' // necessary for authZ; remove after

   testImplementation 'org.springframework.boot:spring-boot-starter-test'
   testImplementation 'org.springframework.security:spring-security-test'


   // transitive dependencies version force
   // on next upgrade, revise if those still need to be set explicitly
   implementation versions.'com.squareup.okio:okio'
   implementation versions.'commons-io:commons-io'
   implementation versions.'org.json:json'
   implementation versions.'org.yaml:snakeyaml'
   implementation versions.'io.swagger.core.v3:swagger-annotations'
   implementation versions.'io.swagger.core.v3:swagger-models'
   implementation versions.'io.micrometer:micrometer-core'
   testImplementation versions.'org.awaitility:awaitility'
   testImplementation versions.'com.github.tomakehurst:wiremock'
   testImplementation versions.'org.mockito:mockito-core'
   testImplementation versions.'net.bytebuddy:byte-buddy'
   testImplementation 'org.junit.jupiter:junit-jupiter-api'
   testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}
